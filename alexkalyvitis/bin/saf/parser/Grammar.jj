/**
 * JavaCC template file created by SF JavaCC plugin 1.5.17+ wizard for JavaCC 1.5.0+
 */
options
{
  JDK_VERSION = "1.5";
  static = false;
}

PARSER_BEGIN(Saf)

package saf.parser;

import java.util.List;
import java.util.ArrayList;

import saf.ast.*;

public class Saf {   }

PARSER_END(Saf)

SKIP : // Whitespace
{
    " "
|   "\r"
|   "\t"
|   "\n"
|   "\r\n"
}

TOKEN :
{
	< ASSIGNMENT : "=" >
|   < AND : "and" >
|   < OR : "or" >
|   < CHOOSE : "choose" >
|   < IDENTIFIER : ["a"-"z","A"-"Z","_"](["a"-"z","A"-"Z","_","0"-"9"])* >
|   < PROPERTYVALUE : (["0"-"9"])+ >
|   < OPENPARENTHESIS : "(" >
|   < CLOSEPARENTHESIS : ")" >
|   < OPENCURLYBRACKET : "{" >
|   < CLOSECURLYBRACKET : "}" >
|   < OPENSQAREBRACKET : "[" >
|   < CLOSESQAREBRACKET : "]" >
}

List<Fighter> Parse() :
{
	ArrayList<Fighter> fighters = new ArrayList<Fighter>();
	Fighter fighter = new Fighter();
	 
	Token fighterName = new Token();
	Strength fighterStrength = new Strength();
	List<Behavior> fighterBehaviors = new ArrayList<Behavior>();
}
{
    (
        (fighterName = < IDENTIFIER >{ fighter = new Fighter(fighterName.toString()); }) 
        (fighterName = < IDENTIFIER >{ fighter.appendName(fighterName.toString()); })* 
	    (< OPENCURLYBRACKET >)
		(
		    LOOKAHEAD(2)
		    ( fighterStrength = Strength() { fighter.addStrength(fighterStrength); }) 
		    |
		    ( fighterBehaviors = Behaviors() { fighter.addBehaviors(fighterBehaviors); })
		)*
		( <  CLOSECURLYBRACKET >)
		{ fighters.add(fighter); }
	)*
	< EOF >
	{ return fighters; }
}

Strength Strength() :
{
    Token name = new Token();
    Token value = new Token();
}
{
    (name = < IDENTIFIER >)
    < ASSIGNMENT >
    (value = < PROPERTYVALUE >{ return new Strength(name.toString(),Integer.valueOf(value.toString())); })
}

List<Behavior> Behaviors() :
{
    Condition condition = new Condition();

    List<Move> moves = new ArrayList<Move>();
    List<Attack> attacks = new ArrayList<Attack>();

    List<Behavior> behaviors = new ArrayList<Behavior>();
}
{
    (condition = Condition())
    < OPENSQAREBRACKET >
    (moves = Moves())
    (attacks = Attacks())
    < CLOSESQAREBRACKET >
    {
        for(Move move : moves)
        {
            for(Attack attack : attacks)
            {
                behaviors.add(new Behavior(condition,move,attack));
            }
        }
        return behaviors;
    }
}
Condition Condition() :
{
    Token conditionToken = new Token();
    Token secondConditionToken = new Token();
    
    Condition condition;
}
{
    LOOKAHEAD(2)
    (
        LOOKAHEAD(2)
        (
            conditionToken = < IDENTIFIER >
	        < AND >
	        secondConditionToken = < IDENTIFIER >
	        {
	            condition = new AndCondition();
	            ((AndCondition)condition).setCondition(conditionToken.toString());
	            ((AndCondition)condition).setAndCondition(secondConditionToken.toString());
	            return condition;
	        }
	    )
		|
		(
		    conditionToken = < IDENTIFIER >
	    	< OR >
	    	secondConditionToken = < IDENTIFIER >
	    	{
	    	    condition = new OrCondition();
	            ((OrCondition)condition).setCondition(conditionToken.toString());
	            ((OrCondition)condition).setOrCondition(secondConditionToken.toString());
	            return condition;
	    	}
		)
	)
	|
	(
	    conditionToken = < IDENTIFIER >
		{
		    condition = new Condition();
		    condition.setName(conditionToken.toString());
		    return condition;
		}
	)
}

List<Move> Moves() :
{
	Token moveToken = new Token();
	List<Move> moves = new ArrayList<Move>(); 
}
{
    (moveToken = < IDENTIFIER >) { moves.add(new Move(moveToken.toString())); return moves; }
    |
    (< CHOOSE >)
    (< OPENPARENTHESIS >)
    (
        (moveToken = < IDENTIFIER >){ moves.add(new Move(moveToken.toString())); }
    )+
    (< CLOSEPARENTHESIS >)
    { return moves; }
}

List<Attack> Attacks() :
{
    Token attackToken = new Token();
    ArrayList<Attack> attacks = new ArrayList<Attack>();
}
{
    (attackToken = < IDENTIFIER >) { attacks.add(new Attack(attackToken.toString())); return attacks; }
    |
    (< CHOOSE >)
    (< OPENPARENTHESIS >)
    (
        (attackToken = < IDENTIFIER >){ attacks.add(new Attack(attackToken.toString())); }
    )+
    (< CLOSEPARENTHESIS >)
    { return attacks; }
}